* COMMENTARY (The RAND Blog)
* Title: Laid off more, hired less: The Story of Black Workers in this Recession

* Commentary written by: Jhacova Williams

* Stata code written by: Daniel Schwam

* This Do file uses a pre-generated data dictionary from IPUMS to format the CPS extract.

* NOTE(S): 
* 1. You need to set the Stata working directory to the path where the data file 
*    is located.
* 2. You also need to ensure that the CPS data extract file name reflects the file 
*    name of your query (see line 67 of "Format CPS extract.do").
* 3. See the end of this Do file for a list of variables in our IPUMS extract.

* Must install grc1leg function from user vwiggins. 
* Search for package using "search grc1leg".

* Set working directory
cd []

global cpsextract 00042

* Format raw CPS data from IPUMS
do "Format CPS extract.do"

* Transitions from temporary to permanent layoff

* Load analytic file
use "cps_${cpsextract}.dta", clear

* Subset to 2020
drop if year < 2020

* Identify linked individuals across months
do "Long-Validation-Code.do"

* Drop unmatched/unlinked cases
drop if matched_keep == 0

* Set panel structure
gen month2 = month
gen date = ym(year, month2)
format date %tm
sort cpsidp date
xtset cpsidp date

* Ensure that individuals are at least 19 at the start of the analysis
by cpsidp: egen min_age = min(age)
drop if min_age < 19

* Drop children and people in the Armed Forces
drop if popstat != 1

* Generate a variable indicating the employment state of the individual in the
* current and following months

* Individuals are considered employed if they were classified as "at work" (empstat == 10)
* or the have a job and were absent from work the previous week for a specified 
* reason (whyabsnt != 15)
gen state_t0 = 1 if empstat == 10 | (empstat == 12 & whyabsnt != 15)


* Individuals classified by layoff status

* The CPS has misclassified workers that were not at work that should have been
* classified as on temporary layoff.
* Source: https://www.brookings.edu/blog/up-front/2020/06/30/who-are-the-potentially-misclassified-in-the-employment-report/#cancel

* Temporary layoff (including employed but not at work for other reasons)
replace state_t0 = 2 if empstat == 12 & whyabsnt == 15
replace state_t0 = 2 if absent == 2 & uh_lay6m_b1 == 1

* Permanent layoff (including people that are permanently unemployed)
replace state_t0 = 3 if absent == 2 & uh_lay6m_b1 == -1
replace state_t0 = 3 if absent == 1 & empstat == 21

* Other unemployed (i.e., new worker)
replace state_t0 = 4 if empstat == 22

* Not in the labor force (NILF)
replace state_t0 = 5 if labforce == 1

* Variable label
label define emp_state 1 "Employed" 2 "Temporary layoff" 3 "Permanent layoff/unemployment" 4 "Unemployed, new worker" 5 "NILF"
label values state_t0 emp_state

* Distribution to state in time t = 0
tab state_t0, m

* Employment state in time t = 1
* Employed
by cpsidp: gen state_t1 = 1 if F.empstat == 10 | F.empstat == 12 & F.whyabsnt != 15

* Employed, not at work for other reason

* Temporary layoff (including employed not at work for other reasons)
by cpsidp: replace state_t1 = 2 if F.empstat == 12 & F.whyabsnt == 15
by cpsidp: replace state_t1 = 2 if F.absent == 2 & F.uh_lay6m_b1 == 1

* Permanent layoff (including people that are permanently unemployed)
by cpsidp: replace state_t1 = 3 if F.absent == 2 & F.uh_lay6m_b1 == -1
by cpsidp: replace state_t1 = 3 if F.absent == 1 & F.empstat == 21

* Other unemployed (i.e., new worker)
by cpsidp: replace state_t1 = 4 if F.empstat == 22

* Not in the labor force (NILF)
by cpsidp: replace state_t1 = 5 if F.labforce == 1

* Assign labels
label values state_t1 emp_state

* Distribution to state in time t = 1
tab state_t1, m

* Drop individuals that only appear once (i.e., no longer in CPS) 
drop if state_t1 == .

* Joint distribution
tab state_t0 state_t1, m

cap drop n
gen n = 1


* Generate race/ethinicity buckets
gen race_eth = "White, non-Hispanic" if race == 100 & hispan == 0
replace race_eth = "Black, non-Hispanic" if race == 200 & hispan == 0
replace race_eth = "Other, non-Hispanic" if race >= 300 & hispan == 0
replace race_eth = "Hispanic" if hispan != 0
tab race_eth, m

* Determine if we have sufficient sample sizes over time in each race/eth-sex
* combination.

* By race
preserve

	collapse (rawsum) samp_unweighted=n (sum) samp_weighted=n [pweight=wtfinl], by(race_eth year month)
	format samp* %15.0f
	
	* Export to excel
	export excel "laidoffmore_hiredless.xlsx", sheetreplace sheet("Sample Size - Overall") firstrow(variables)
restore

* Now check if we have sufficient sample sizes for our employment states generated
* above.

* By race
foreach re in "White, non-Hispanic" "Black, non-Hispanic" "Other, non-Hispanic" "Hispanic" {

	if "`re'" == "White, non-Hispanic" {
		local r "W-NH"
	}
	else if "`re'" == "Black, non-Hispanic" {
		local r "B-NH"
	}
	else if "`re'" == "Other, non-Hispanic" {
		local r "Oth-NH"
	}
	else {
		local r "Hisp"
	}
	
	preserve
	
		* Subset to sample of interest
		keep if race_eth == "`re'"
		
		* Calculate sample size by employment states
		collapse (sum) n, by(year month state_t0 state_t1)
		
		* Reshape to wide
		quietly reshape wide n, i(year month state_t0) j(state_t1)
		
		* Drop July
		drop if month == 8
		
		* Label variables
		label var n1 "Employed" 
		label var n2 "Temporary layoff (incl. employed but not at work for other reason)" 
		label var n3 "Permanent layoff/unemployment" 
		label var n4 "Unemployed, new worker"
		label var n5 "NILF"
		
		* Export to Excel
		export excel "laidoffmore_hiredless.xlsx", sheetreplace sheet("Sample Size - `r'") firstrow(varlabels)

	restore
	
}

* Transition rates by employment states
preserve

	collapse (sum) n [pweight=lnkfw1mwt], by(race_eth state_t0 state_t1 year month)
	
	* Reshape to wide
	quietly reshape wide n, i(year month race_eth state_t0) j(state_t1)
	
	tempfile transitiondata
	save `transitiondata'

restore

preserve

	* Calculate population by employment state in time t = 0 (wtfinl or lnkfw1mwt here?)
	collapse (sum) n [pweight=lnkfw1mwt], by(race_eth state_t0 year month)
	ren n t

	* Combine with transition rates
	merge 1:1 race_eth state_t0 year month using `transitiondata'
	drop _merge

	drop if month == 8

	format t n* %15.0f

	* Calculate percents
	foreach var of varlist n* {
		gen pct_`var' = (`var' / t) * 100
	}

	* Employment states of interest in time t == 0
	* keep if state_t0 == 2 | state_t0 == 3

	drop t

	* Label variables
	label var n1 "# Employed" 
	label var n2 "# Temporary layoff (incl. employed but not at work for other reason)" 
	label var n3 "# Permanent layoff/unemployment" 
	label var n4 "# Unemployed, new worker"
	label var n5 "# NILF"
	label var pct_n1 "Employed" 
	label var pct_n2 "Temporary layoff (incl. employed but not at work for other reason)" 
	label var pct_n3 "Permanent layoff/unemployment" 
	label var pct_n4 "Unemployed, new worker"
	label var pct_n5 "NILF"

	* Export to Excel
	export excel "laidoffmore_hiredless.xlsx", sheetreplace sheet("TM - race ethn") firstrow(varlabels)
	
	tempfile tm_race
	save `tm_race'
	
	* Figure 1: Transitions Among Employed Workers by Race, March-April 2020
	* Subset to Black and White workers, March to April transition, and 
	* employed status in March 2020
	keep if inlist(race_eth, "Black, non-Hispanic", "White, non-Hispanic")
	keep if month == 3
	keep if state_t0 == 1
	
	* Keep relevant variables for figure
	keep race_eth pct_*
	
	* Reshape for figure display and recode race/ethnicity variable
	quietly reshape long pct_n, i(race_eth) j(state_t1)
	replace race_eth = "Blacks" if race_eth == "Black, non-Hispanic"
	replace race_eth = "Whites" if race_eth == "White, non-Hispanic"
	quietly reshape wide pct_n, i(state_t1) j(race_eth) string
	ren pct_n* *
	
	* Drop unemployed, new workers category
	drop if state_t1 == 4
	
	* Label variables
	label var state_t1 "Employment state in April 2020"
	label var Blacks "Black workers"
	label var White "White workers"
	
	* Export figure data to Excel
	export excel "laidoffmore_hiredless.xlsx", sheetreplace sheet("figure1") firstrow(varlabels)
	
	* Plot and save (need to transpose first for graphing purposes)
	xpose, clear varname
	ren (v1 v2 v3 v4) (employed templayoff permlayoff nilf)
	drop if _varname == "state_t1"
	replace _varname = "Black Workers" if _varname == "Blacks"
	replace _varname = "White Workers" if _varname == "Whites"
	graph bar employed templayoff permlayoff nilf, over(_varname) stack graphregion(color(white)) legend(order(1 "Employed" 2 "Temporary Layoff" 3 "Permanent Layoff/Unemployed" 4 "NILF")) blabel(bar, format("%2.0f") position(center) color(white))
	graph export "laidoffmore_hiredless_figure1.pdf", replace
restore

* Figure 3: Percent of Temporary Layoffs to Employment vs. Permanent Layoffs

* Re-load transition matrix data
use `tm_race', clear

* Subset to Black and White workers and temporary layoff status in month t = 0
keep if inlist(race_eth, "Black, non-Hispanic", "White, non-Hispanic") & state_t0 == 2

drop if month < 4

* Keep relevant variables for figure (transitions to employment or permanent layoff/unemployment)
keep race_eth month pct_n1 pct_n3

* Recode race/ethnicity variable for reshaping
replace race_eth = "Blacks" if race_eth == "Black, non-Hispanic"
replace race_eth = "Whites" if race_eth == "White, non-Hispanic"

* Reshape to wide
quietly reshape wide pct_n1 pct_n3, i(month) j(race_eth) string

* Rename, label, and order variables
ren (pct_n1* pct_n3*) (Employed* PermLayoff*)
label var EmployedBlacks "Moved to Employment: Black Workers"
label var PermLayoffBlacks "Moved to Permanent Layoff: Black Workers"
label var EmployedWhites "Moved to Employment: White Workers"
label var PermLayoffWhites "Moved to Permanent Layoff: White Workers"
order month EmployedBlacks EmployedWhites PermLayoffBlacks PermLayoffWhites

* Relabel month variable for figure display
label define month_tm 4 "April - May" 5 "May - June" 6 "June - July" 7 "July - August", replace
label value month month_tm

* Export figure data to Excel
export excel "laidoffmore_hiredless.xlsx", sheetreplace sheet("figure3") firstrow(varlabels)

* Plot and save
twoway (connected EmployedBlacks month) (connected EmployedWhites month), graphregion(color(white)) legend(order(1 "Black Workers" 2 "White Workers")) title("Moved to Employment") xlabel(4 "Apr-May" 5 "May-Jun" 6 "Jun-Jul" 7 "Jul-Aug")
graph save "laidoffmore_hiredless_figure3_part1.gph", replace

twoway (connected PermLayoffBlacks month) (connected PermLayoffWhites month), graphregion(color(white)) legend(order(1 "Black Workers" 2 "White Workers")) title("Moved to Permanent Layoff/Unemployment") xlabel(4 "Apr-May" 5 "May-Jun" 6 "Jun-Jul" 7 "Jul-Aug")
graph save "laidoffmore_hiredless_figure3_part2.gph", replace

grc1leg "laidoffmore_hiredless_figure3_part1.gph" "laidoffmore_hiredless_figure3_part2.gph", graphregion(color(white)) rows(2)
graph export "laidoffmore_hiredless_figure3.pdf", replace
	

/*
* Variables in our IPUMS extract
Type	Variable	Label
H	YEAR	Survey year
H	SERIAL	Household serial number
H	MONTH	Month
H	HWTFINL	Household weight, Basic Monthly
H	CPSID	CPSID, household record
H	ASECFLAG	Flag for ASEC
H	MISH	Month in sample, household level
H	REGION	Region and division
H	STATEFIP	State (FIPS code)
H	METAREA	Metropolitan area
H	COUNTY	FIPS county code
H	FAMINC	Family income of householder
H	HRHHID	Household ID, part 1
H	HRHHID2	Household ID, part 2
P	PERNUM	Person number in sample unit
P	WTFINL	Final Basic Weight
P	CPSIDP	CPSID, person record
P	RELATE	Relationship to household head
P	AGE	Age
P	SEX	Sex
P	RACE	Race
P	POPSTAT	Adult civilian, armed forces, or child
P	HISPAN	Hispanic origin
P	EMPSTAT	Employment status
P	LABFORCE	Labor force status
P	OCC	Occupation
P	IND	Industry
P	CLASSWKR	Class of worker
P	UHRSWORKT	Hours usually worked per week at all jobs
P	UHRSWORK1	Hours usually worked per week at main job
P	UHRSWORK2	Hours usually worked per week, other job(s)
P	AHRSWORKT	Hours worked last week
P	AHRSWORK1	Hours worked last week, main job
P	AHRSWORK2	Hours worked last week, other job(s)
P	ABSENT	Absent from work last week
P	DURUNEM2	Continuous weeks unemployed, intervalled
P	DURUNEMP	Continuous weeks unemployed
P	WHYUNEMP	Reason for unemployment
P	WHYABSNT	Reason for absence from work
P	WHYPTLWK	Reason for working part time last week
P	WKSTAT	Full or part time status
P	EDUC	Educational attainment recode
P	EDUC99	Educational attainment, 1990
P	EDDIPGED	High school or GED
P	SCHLCOLL	School or college attendance
P	LNKFW1MWT	Longitudinal weight for two adjacent months (BMS only)
P	PANLWT	Month-to-month panel weight
P	HOURWAGE	Hourly wage
P	PAIDHOUR	Paid by the hour
P	UNION	Union membership
P	EARNWEEK	Weekly earnings
P	UHRSWORKORG	Usual hours worked per week, outgoing rotation groups
P	WKSWORKORG	Weeks worked per year, outgoing rotation groups
P	ELIGORG	(Earnings) eligibility flag
P	OTPAY	Usually receive overtime, tips, or commissions
P	UH_MLR_B3	Monthly labor force recode (formerly esr), 1994-2020_08
P	UH_LAY6M_B1	Recall to work within 6 months, 1994-2020_08
P	UH_LAYDT_B1	Return to work date, 1994-2020_08
P	UH_LAYOFF_B1	On layoff last week, 1994-2020_08
*/
